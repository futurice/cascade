apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.3'
    publishNonDefault true
    // Gradle work-around, comment#35 https://code.google.com/p/android/issues/detail?id=52962

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "0.3.8"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        // InvalidPackage is for Okio, https://github.com/square/okio/issues/58
        // OldTargetApi and GradleDependency prevent errors a newer Android API version is available from Google but not yet supported by Travis CI
        disable 'InvalidPackage', 'OldTargetApi', 'GradleDependency'
    }

    packagingOptions {
        exclude "LICENSE.txt" // Including these multiple times from different modules can break the build
        exclude 'META-INF/LICENSE.txt'
    }

    dexOptions {
        incremental false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        debug {
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:support-annotations:24.2.1'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.1.0'
//    testCompile 'org.jmockit:jmockit:1.22'

    androidTestCompile('org.hamcrest:hamcrest-library:1.3')
    androidTestCompile('com.google.dexmaker:dexmaker:1.2') {
        exclude group: 'org.hamcrest'
    }
    androidTestCompile('com.google.dexmaker:dexmaker-mockito:1.2') {
        exclude group: 'org.hamcrest'
    }
    androidTestCompile('org.mockito:mockito-core:2.1.0') {
        exclude group: 'org.hamcrest'
    }
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
        // Include other used support libraries here
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        //noinspection GroovyAssignabilityCheck
        classpath = files(variant.javaCompile.classpath.files) + files(android.bootClasspath)
        options.links("http://docs.oracle.com/javase/8/docs/api/");
        options.links("http://developer.android.com/reference");
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
        failOnError false
    }
}
