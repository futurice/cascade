apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion '25'
    publishNonDefault true
    // Gradle work-around, comment#35 https://code.google.com/p/android/issues/detail?id=52962

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "0.3.8"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        // InvalidPackage is for Okio, https://github.com/square/okio/issues/58
        // OldTargetApi and GradleDependency prevent errors a newer Android API version is available from Google but not yet supported by Travis CI
        disable 'InvalidPackage', 'OldTargetApi', 'GradleDependency'
    }

    packagingOptions {
        exclude "LICENSE.txt" // For com.android.support.test:runner:0.2
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        debug {
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:support-annotations:25.0.0'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.jmockit:jmockit:1.22'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source

        title = "Android Cascade - Concurrent Functional-Reactive Library"
        options.links("http://docs.oracle.com/javase/8/docs/api/");
        options.linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference");
        options.addStringOption('Xdoclint:none', '-quiet')
        options.links("http://docs.oracle.com/javase/8/docs/api/");
        options.links("http://developer.android.com/reference");
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath += files(variant.javaCompile.classpath.files)
        classpath += files(android.getBootClasspath())
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
        failOnError true
    }
}
