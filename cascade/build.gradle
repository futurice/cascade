apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    publishNonDefault true

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.compileSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        // InvalidPackage is for Okio, https://github.com/square/okio/issues/58
        // OldTargetApi and GradleDependency prevent errors a newer Android API version is available from Google but not yet supported by Travis CI
        disable 'InvalidPackage', 'OldTargetApi', 'GradleDependency'
    }

    packagingOptions {
        exclude "LICENSE.txt" // Including these multiple times from different modules can break the build
        exclude 'META-INF/LICENSE.txt'
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }

    buildTypes {
        debug {
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:" + rootProject.supportVersion;
    compile "com.android.support:support-annotations:" + rootProject.supportVersion;
    compile 'com.squareup.okhttp3:okhttp:' + rootProject.okhttpVersion;
    testCompile 'junit:junit:' + rootProject.junitVersion;
    testCompile 'org.jmockit:jmockit:' + rootProject.jmockitVersion;
    androidTestCompile 'junit:junit:' + rootProject.junitVersion;
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:' + rootProject.espressoVersion;
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:' + rootProject.espressoVersion) {
        exclude group: 'com.android.support', module: 'support-annotations'
        // Include other used support libraries here
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source

        title = "Android Cascade - Concurrent Functional-Reactive Library"
        options.links("http://docs.oracle.com/javase/8/docs/api/");
        options.linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference");
        options.addStringOption('Xdoclint:none', '-quiet')
        options.links("http://docs.oracle.com/javase/8/docs/api/");
        options.links("http://developer.android.com/reference");
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath += files(variant.javaCompile.classpath.files)
        classpath += files(android.getBootClasspath())
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
        failOnError true
    }
}
