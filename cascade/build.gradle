apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

android {
    publishNonDefault true
    buildToolsVersion rootProject.buildToolsVersion
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.compileSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        // InvalidPackage is for Okio, https://github.com/square/okio/issues/58
        // OldTargetApi and GradleDependency prevent errors a newer Android API version is available from Google but not yet supported by Travis CI
        disable 'InvalidPackage', 'OldTargetApi', 'GradleDependency'
    }

//    packagingOptions {
//        exclude "LICENSE.txt" // For com.android.support.test:runner:0.2
//    }

//    testOptions {
//        unitTests.returnDefaultValues = true
//    }

    buildTypes {
        debug {
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:" + rootProject.supportVersion;
    compile "com.android.support:support-annotations:" + rootProject.supportVersion;
    compile 'com.squareup.okhttp3:okhttp:' + rootProject.okhttpVersion;
    testCompile 'junit:junit:' + rootProject.junitVersion;
    testCompile 'org.mockito:mockito-core:' + rootProject.mockitoVersion;
    testCompile 'org.jmockit:jmockit:' + rootProject.jmockitVersion;
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile('com.android.support.test.espresso:espresso-core:' + rootProject.espressoVersion, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        //noinspection GroovyAssignabilityCheck
        classpath = files(variant.javaCompile.classpath.files) + files(android.bootClasspath)
        options.links("http://docs.oracle.com/javase/8/docs/api/");
        options.links("http://developer.android.com/reference");
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
        failOnError false
    }
}
